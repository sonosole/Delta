export FastCTC, seqfastctc
export FastCTCGreedySearch
export FastCTCGreedySearchWithTimestamp
export CRNN_FastCTC_With_Softmax


function seqfastctc(seq, blank::Int=1)
    L = length(seq)       # sequence length
    N = 2 * L + 1         # topology length
    label = zeros(Int, N)
    label[1:2:N] .= blank
    label[2:2:N] .= seq
    return label
end


"""
    FastCTC(p::Array{T,2}, seqlabel; blank::Int=1) where T

# Topology Example
     â”Œâ”€â–ºâ”€â”    â”Œâ”€â–ºâ”€â”    â”Œâ”€â–ºâ”€â”    â”Œâ”€â–ºâ”€â”    â”Œâ”€â–ºâ”€â”    â”Œâ”€â–ºâ”€â”    â”Œâ”€â–ºâ”€â”
    â”Œâ”´â”€â”€â”€â”´â”  â”Œâ”´â”€â”€â”€â”´â”  â”Œâ”´â”€â”€â”€â”´â”  â”Œâ”´â”€â”€â”€â”´â”  â”Œâ”´â”€â”€â”€â”´â”  â”Œâ”´â”€â”€â”€â”´â”  â”Œâ”´â”€â”€â”€â”´â”
    â”‚blankâ”œâ”€â–ºâ”‚  C  â”œâ”€â–ºâ”‚blankâ”œâ”€â–ºâ”‚  A  â”œâ”€â–ºâ”‚blankâ”œâ”€â–ºâ”‚  T  â”œâ”€â–ºâ”‚blankâ”‚
    â””â”€â”€â”€â”€â”€â”˜  â””â”€â”€â”€â”€â”€â”˜  â””â”€â”€â”€â”€â”€â”˜  â””â”€â”€â”€â”€â”€â”˜  â””â”€â”€â”€â”€â”€â”˜  â””â”€â”€â”€â”€â”€â”˜  â””â”€â”€â”€â”€â”€â”˜
"""
function FastCTC(p::Array{TYPE,2}, seqlabel; blank::Int=1) where TYPE
    seq  = seqfastctc(seqlabel, blank)
    Log0 = LogZero(TYPE)   # approximate -Inf of TYPE
    ZERO = TYPE(0)         # typed zero,e.g. Float32(0)
    S, T = size(p)         # assert p is a 2-D tensor
    L = length(seq)        # topology length with blanks
    r = fill!(Array{TYPE,2}(undef,S,T), ZERO)    # ğœ¸ = p(s[k,t] | x[1:T]), k in softmax's indexing

    if L == 1
        r[blank,:] .= TYPE(1)
        return r, - sum(log.(p[blank,:]))
    end

    a = fill!(Array{TYPE,2}(undef,L,T), Log0)    # ğœ¶ = p(s[k,t], x[1:t]), k in FastCTC topology's indexing
    b = fill!(Array{TYPE,2}(undef,L,T), Log0)    # ğ›ƒ = p(x[t+1:T] | s[k,t]), k in FastCTC topology's indexing
    a[1,1] = log(p[seq[1],1])
    a[2,1] = log(p[seq[2],1])
    b[L-1,T] = ZERO
    b[L  ,T] = ZERO

    # --- forward in log scale ---
    for t = 2:T
        Ï„ = t-1
        first = max(1, L-2*(T-t)-1)
        lasst = min(2*t, L)
        for s = first:lasst
            if sâ‰ 1
                a[s,t] = LogSum2Exp(a[s,Ï„], a[s-1,Ï„])
            else
                a[s,t] = a[s,Ï„]
            end
            a[s,t] += log(p[seq[s],t])
        end
    end

    # --- backward in log scale ---
    for t = T-1:-1:1
        Ï„ = t+1
        first = max(1, L-2*(T-t)-1)
        lasst = min(2*t, L)
        for s = first:lasst
            Q = b[s,Ï„] + log(p[seq[s],Ï„])
            if sâ‰ L
                b[s,t] = LogSum2Exp(Q, b[s+1,Ï„] + log(p[seq[s+1],Ï„]))
            else
                b[s,t] = Q
            end
        end
    end

    logsum = LogSum2Exp(a[1,1] + b[1,1], a[2,1] + b[2,1])
    g = exp.((a + b) .- logsum)

    # reduce first line of g
    r[blank,:] .+= g[1,:]
    # reduce rest lines of g
    for n = 1:div(L-1,2)
        s = n<<1
        r[seq[s],:] .+= g[s,  :]
        r[blank, :] .+= g[s+1,:]
    end

    return r, -logsum
end


"""
    FastCTCGreedySearch(x::Array; blank::Int=1, dim::Int=1) -> hypothesis
remove repeats and blanks of argmax(x, dims=dims)
"""
function FastCTCGreedySearch(x::Array; blank::Int=1, dim::Int=1)
    hyp = Vector{Int}(undef, 0)
    idx = argmax(x, dims=dim)
    for t = 1:length(idx)
        previous = idx[tâ‰ 1 ? t-1 : t][1]
        current  = idx[t][1]
        if !((tâ‰ 1 && current==previous) || (current==blank))
            push!(hyp, current)
        end
    end
    return hyp
end


"""
    FastCTCGreedySearchWithTimestamp(x::Array; blank::Int=1, dim::Int=1) -> hypothesis, timestamp
"""
function FastCTCGreedySearchWithTimestamp(x::Array; blank::Int=1, dim::Int=1)
    hyp = Vector{Int}(undef, 0)
    stp = Vector{Float32}(undef, 0)
    idx = argmax(x, dims=dim)
    T   = length(idx)
    for t = 1:T
        previous = idx[tâ‰ 1 ? t-1 : t][1]
        current  = idx[t][1]
        if !((tâ‰ 1 && current==previous) || (current==blank))
            push!(hyp, current)
            push!(stp, t / T)
        end
    end
    return hyp, stp
end



function CRNN_FastCTC_With_Softmax(x::Variable{Array{T}},
                                   seqlabels::Vector;
                                   blank::Int=1,
                                   weight::Float64=1.0,
                                   reduction::String="seqlen") where T
    featdims, timesteps, batchsize = size(x)
    loglikely = zeros(T, batchsize)
    p = softmax(áµ›(x); dims=1)
    r = zero(áµ›(x))

    Threads.@threads for b = 1:batchsize
        r[:,:,b], loglikely[b] = FastCTC(p[:,:,b], seqlabels[b], blank=blank)
    end

    Î” = p - r
    reduce3d(Î”, loglikely, seqlabels, reduction)

    if x.backprop
        function CRNN_FastCTC_With_Softmax_Backward()
            if need2computeÎ´!(x)
                if weight==1.0
                    Î´(x) .+= Î”
                else
                    Î´(x) .+= Î” .* weight
                end
            end
        end
        push!(graph.backward, CRNN_FastCTC_With_Softmax_Backward)
    end
    return sum(loglikely)
end
